import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

version = "2.7.0-SNAPSHOT"

dependencies {
    implementation 'com.github.NLthijs48:InteractiveMessenger:e7749258ca'
    implementation 'com.github.NLthijs48:BukkitDo:819d51ec2b'
    implementation project(':areashop-interface')
    implementation project(':areashop-bukkit-1_13')
    implementation project(':areashop-worldedit-7')
    implementation project(':areashop-worldguard-7')
    implementation project(':areashop-fastasyncworldedit')
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.2.5'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.5'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
}

processResources {

    from (sourceSets.main.resources) {
        filter ReplaceTokens, tokens: [version: project.version]
        duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    }
}

assemble {
    dependsOn(shadowJar)
}

jar {
    archiveClassifier.set('original')
}

shadowJar {
    archiveClassifier.set('')
    setArchivesBaseName('AreaShop')
    String base = "me.wiefferink.areashop.libraries"
    relocate('me.wiefferink.interactivemessenger', "${base}.interactivemessenger")
    relocate('me.wiefferink.bukkitdo', "${base}.bukkitdo")
}

description = 'AreaShop'
